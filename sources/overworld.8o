:const player_shift			8
:const neg_player_shift		-8

: overworld_start
	map_render
	va := 24
	vb := 24
	i := long tile_player_front_data
	sprite va vb 0

	loop
		v0 := key

		sprite va vb 0

		if v0 == 5 then
			move_up

		if v0 == 7 then
			move_left

		if v0 == 8 then
			move_down

		if v0 == 9 then
			move_right

		sprite va vb 0
	again
	return


: hit_test
	i := player_pos
	save va - vb
	map_is_wall

	i := v0_store
	save v0 - v0
	va := 0
	sfx_play
	i := v0_store
	load v0 - v0

	i := player_pos
	load va - vb
	return


: check_objects
	map_get_screen_index
	va := v0
	va >>= va

	i := long map_object_data
	i += va
	i += va
	load v0 - v1
	i := map_current_objects_ptr
	save v0 - v1
	0xf0 0x00
	: map_current_objects_ptr
	0 0

	load v0 - v2
	if v0 != 0 begin
		i := long tile_robot_engineer_front_data
		sprite v1 v2 0
	else
		v1 := 0
		v2 := 0
	end
	i := object_pos
	save v1 - v2

	return


: move_right
	if va >= 112 begin
		i := player_y
		save vb - vb
		map_scroll_right
		check_objects
		i := player_y
		load vb - vb
		va := 0
	else
		va += 8
		vb += 12
		hit_test
		va += -8
		vb += -12
		if v0 == 0 then va += player_shift
	end
	i := long tile_player_right_data
	return

: move_left
	if va <= 0 begin
		i := player_y
		save vb - vb
		map_scroll_left
		check_objects
		i := player_y
		load vb - vb
		va := 112
	else
		vb += 12
		hit_test
		vb += -12
		if v0 == 0 then va += neg_player_shift
	end
	i := long tile_player_left_data
	return


: move_down
	if vb >= 44 begin
		i := player_x
		save va - va
		map_scroll_down
		check_objects
		i := player_x
		load va - va
		vb := 0
	else
		vb += 16
		hit_test
		vb += -16
		if v0 == 0 then vb += player_shift
	end
	i := long tile_player_front_data
	return


: move_up
	if vb <= 0 begin
		i := player_x
		save va - va
		map_scroll_up
		check_objects
		i := player_x
		load va - va
		vb := 48
	else
		vb += 10
		hit_test
		vb += -10
		if v0 == 0 then vb += neg_player_shift
	end
	i := long tile_player_back_data
	return


: object_pos 0x00 0x00
