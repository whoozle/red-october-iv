:const player_shift			8
:const neg_player_shift		-8

: overworld_start
	map_render
	player_draw

	game_save

	loop
		v0 := OCTO_KEY_W
		if v0 key then
			move_up

		v0 := OCTO_KEY_A
		if v0 key then
			move_left

		v0 := OCTO_KEY_S
		if v0 key then
			move_down

		v0 := OCTO_KEY_D
		if v0 key then
			move_right
	again
	return

:const player_dir_front 0
:const player_dir_left 1
:const player_dir_right 2
:const player_dir_back 3

: load_player_pos
	i := player_pos
	load va - vb
	return

: save_player_pos
	i := player_pos
	save va - vb
	return

: player_dir
	:byte player_dir_front

: player_set_dir_v0
	i := player_dir
	save v0 - v0
	return

: player_draw
	load_player_pos
	i := player_dir
	load v0 - v0
	i := long tile_player_front_data
	v1 := 64
: player_draw_next_tile
	if v0 == 0 then
		jump player_draw_sprite
	i += v1
	v0 += -1
	jump player_draw_next_tile
: player_draw_sprite
	sprite va vb 0
	return

: move_sfx
	va := sfx_index_move
	jump sfx_play_sync

: check_door
	i := door_pos
	load v1 - v5
	if v1 != 0 then
		if v2 != 0 begin
			i := player_pos
			load vc - vd
			if vc >= v1 begin
				if vc <= v1 begin
					v2 += 16
					if vb >= v2 begin
						if vb <= v2 begin
							i := player_door_pos
							save v4 - v5
							v0 := v3
							map_scroll_to
							check_objects
							i := player_door_pos
							load va - vb
							i := player_pos
							save va - vb
						end
					end
				end
			end
		end
	return


: check_objects
	map_get_screen_index
	va := v0
	va >>= va

	vb := 0
	loop
	i := long map_object_data
	i += vb
	i += vb
	load v0 - v1
	i := map_current_objects_ptr
	save v0 - v1
	0xf0 0x00
	: map_current_objects_ptr
	0 0
	load v0 - v5
	if v0 == 0x00 begin
		vc += 1
	else
		if va == vc begin
			if v0 == 0x01 begin
				# battle
				i := long tile_robot_engineer_front_data
				sprite v1 v2 0
				i := battle_pos
				save v1 - v2
			else
				if v0 == 0x00 begin
					# reset all object coordinates
					v1 := 0
					v2 := 0
					i := battle_pos
					save v1 - v2
					i := door_pos
					save v1 - v2
					i := power_pos
					save v1 - v2
				else
					if v0 == 0x03 begin
						# door
						i := door_pos
						save v1 - v5
					else
						# powerup
						i := power_pos
						save v1 - v2
						i := long tile_power_up_data
						sprite v1 v2 0
					end
				end
			end
		end
	end

	vb += 1
	if va >= vc then again
	return


: move_right
	player_draw
	if va >= 112 begin
		i := player_y
		save vb - vb
		map_scroll_right
		check_objects
		i := player_y
		load vb - vb
		va := 0
	else
		va += 8
		vb += 12
		map_is_wall
		va += -8
		vb += -12
		if v0 == 0 then va += player_shift
	end
	save_player_pos
	v0 := player_dir_right
	player_set_dir_v0
	player_draw
	jump move_sfx


: move_left
	player_draw
	if va <= 0 begin
		i := player_y
		save vb - vb
		map_scroll_left
		check_objects
		i := player_y
		load vb - vb
		va := 112
	else
		vb += 12
		map_is_wall
		vb += -12
		if v0 == 0 then va += neg_player_shift
		check_door
	end
	save_player_pos
	v0 := player_dir_left
	player_set_dir_v0
	player_draw
	jump move_sfx


: move_down
	player_draw
	if vb >= 44 begin
		i := player_x
		save va - va
		map_scroll_down
		check_objects
		i := player_x
		load va - va
		vb := 0
	else
		vb += 16
		map_is_wall
		vb += -16
		if v0 == 0 then vb += player_shift
	end
	save_player_pos
	v0 := player_dir_front
	player_set_dir_v0
	player_draw
	jump move_sfx


: move_up
	player_draw
	if vb <= 0 begin
		i := player_x
		save va - va
		map_scroll_up
		check_objects
		i := player_x
		load va - va
		vb := 48
	else
		vb += 10
		map_is_wall
		vb += -10
		if v0 == 0 then vb += neg_player_shift
	end
	save_player_pos
	v0 := player_dir_back
	player_set_dir_v0
	player_draw
	jump move_sfx


: battle_pos 0x00 0x00
: door_pos 0x00 0x00 0x00 0x00 0x00
: power_pos 0x00 0x00
: player_door_pos 0x00 0x00
