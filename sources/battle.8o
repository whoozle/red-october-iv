:const sfx_index_menu 0
:const sfx_index_hit 1
:const sfx_index_explosion 2
:const sfx_index_shot 3

:const battle_tick_freq_base	0x10
:const battle_tick_freq_random	0x0f

: battle_start
	clear
	i := battle_object_count
	load v1 - v1
	v2 := v1

	v0 := 255
	i := player_hp
	save v0 - v0
	i := battle_object_hp
	battle_object_fill

	v0 := 0
	v1 := v2
	i := battle_object_t
	battle_object_fill
	battle_draw_hp_bar
	jump battle_menu

: battle_object_fill
	loop
		save v0
		v1 += -1
		if v1 != 0 then
	again
	return

: battle_draw_hp_bar
	i := player_hp
	load v9 - v9
	v9 >>= v9
	v9 >>= v9
	v9 += 1 #1-64

	v8 := 7
	v8 &= v9 #1/8 fraction

	i := battle_draw_hp_bar_frac_sprite_ptr
	v0 := 0x10
	v0 |= v8
	save v0 - v0

	v9 >>= v9
	v9 >>= v9
	v9 >>= v9

	v0 := 120
	v1 := 0
	i := long tile_hp_bar_data

	if v9 == 0 then
		jump battle_draw_hp_bar_frac
	loop
		sprite v0 v1 8
		v1 += 8
		v9 += -1
		if v9 != 0 then
	again

: battle_draw_hp_bar_frac
	if v8 == 0 then
		return

:next battle_draw_hp_bar_frac_sprite_ptr
	sprite v0 v1 8
	return

: battle_enemy_tick
	battle_draw_hp_bar
	i := player_hp
	load v0 - v0
	v1 := random 0x1f
	v0 -= v1
	save v0 - v0
	battle_draw_hp_bar
	return

: battle_object_count
	4

: battle_object_t
	0
	0
	0
	0

: battle_object_hp
	255
	255
	255
	255

: battle_get_attack_tile
	i := long tile_attack_shot_data
	va += va
	va += va
	va += va
	va += va
	i += va
	return

: battle_attack
	battle_get_current_skill_level
	va := v0
	battle_object_attack_animation
	va := random 0x7f
	jump battle_object_damage

: battle_object_attack_animation
	v5 := va
	loop
		v0 := vd
		battle_object_get_x_by_v0
		v6 := v0
		v7 := v1
		v0 := random 0x1f
		v6 += v0
		v6 += -8
		v0 := random 0x1f
		v7 += v0
		v7 += -8

		i := battle_menu_current_skill
		load va - va
		battle_get_attack_tile
		sprite v6 v7 8

		va := sfx_index_shot
		sfx_play_sync

		i := battle_menu_current_skill
		load va - va
		battle_get_attack_tile
		sprite v6 v7 8

		v5 += -1
		if v5 != 0 then
	again
	return

: battle_object_damage
	if va == 0 then #MISS
		return
	i := battle_object_hp
	i += vd
	load v0 - v0
	if v0 == 0 then
		return

	v0 -= va
	if vf == 0 begin
		va := sfx_index_explosion
		sfx_play_sync

		battle_object_draw
		v0 := 0
		i := battle_object_hp
		i += vd
	end
	save v0 - v0

	if v0 == 0 then
		battle_object_draw
	return
