:const sfx_index_menu 0
:const sfx_index_hit 1
:const sfx_index_explosion 2
:const sfx_index_shot 3

:const battle_tick_freq_base	0x10
:const battle_tick_freq_random	0x0f

: battle_start
	i := battle_object_count
	load v1 - v1
	v2 := v1

	v0 := 255
	i := battle_object_hp
	battle_object_fill

	v0 := 0
	v1 := v2
	i := battle_object_t
	battle_object_fill
	jump battle_menu

: battle_object_fill
	loop
		save v0
		v1 += -1
		if v1 != 0 then
	again
	return


: battle_enemy_tick
	return

: battle_object_count
	4

: battle_object_t
	0
	0
	0
	0

: battle_object_hp
	255
	255
	255
	255

: battle_get_attack_tile
	i := long tile_attack_shot_data
	va += va
	va += va
	va += va
	va += va
	i += va
	return

: battle_attack
	v5 := 4
	loop
		v0 := vd
		battle_object_get_x_by_v0
		v6 := v0
		v7 := v1
		v0 := random 0x1f
		v6 += v0
		v6 += -8
		v0 := random 0x1f
		v7 += v0
		v7 += -8

		i := battle_menu_current_skill
		load va - va
		battle_get_attack_tile
		sprite v6 v7 8

		va := sfx_index_shot
		sfx_play_sync

		i := battle_menu_current_skill
		load va - va
		battle_get_attack_tile
		sprite v6 v7 8

		v5 += -1
		if v5 != 0 then
	again
	i := battle_object_hp
	i += vd
	load v0 - v0
	if v0 == 0 then
		return

	v1 := random 0x7f
	if v1 == 0 then #MISS
		return

	v0 -= v1
	if vf == 0 begin
		va := sfx_index_explosion
		sfx_play_sync

		battle_object_draw
		v0 := 0
		i := battle_object_hp
		i += vd
	end
	save v0 - v0

	if v0 == 0 then
		battle_object_draw
	return
