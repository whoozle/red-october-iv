:const battle_tick_freq_base	0x10
:const battle_tick_freq_random	0x0f
:const battle_dt 0x3f

: battle_start
	i := next_battle
	load v0 - v0

	i := long battle_encounter_table
	i += v0
	i += v0
	i += v0
	i += v0
	load v0 - v3

	i := battle_object_type
	save v0 - v3

	v4 := 4
	if v3 == 0 then
		v4 := 3
	if v2 == 0 then
		v4 := 2
	if v1 == 0 then
		v4 := 1

	i := battle_object_count
	save v4 - v4
	v1 := v4

	v0 := 255
	i := player_hp
	save v0 - v0
	i := battle_object_hp
	battle_object_fill
	v1 := 4
	v1 -= v4
	v0 := 0

	if v1 != 0 then
		battle_object_fill

	v0 := 0
	v1 := v4
	i := battle_object_t
	battle_object_fill
	battle_draw_hp_bar
	jump battle_menu

: battle_object_fill
	loop
		save v0
		v1 += -1
		if v1 != 0 then
	again
	return

: battle_enemy_tick
	sfx_wait
	vd := 0
	loop
		i := battle_object_hp
		i += vd
		load v0 - v0
		if v0 == 0 then
			jump battle_enemy_tick_next

		i := battle_object_t
		i += vd

		load v0 - v0
		v0 += battle_dt

		i := battle_object_speed
		i += vd
		load v1 - v1

		if v0 < v1 then
			jump battle_enemy_tick_next

		v2 := 0
		loop
			v0 -= v1
			v2 += 1
			if v0 >= v1 then
		again

		i += vd
		i := battle_object_t
		save v0 - v0

		if v2 == 0 then
			jump battle_enemy_tick_next

		v0 := vd
		battle_object_get_x_by_v0
		i := long tile_attack_highlight_data
		sprite v0 v1 0
#loop
			va := 1
			battle_enemy_attack_animation
			battle_draw_hp_bar
			i := player_hp
			load v0 - v0
			v1 := random 0x1f
			v0 -= v1
			save v0 - v0
			battle_draw_hp_bar
#loop end
		va := 20
		sleep

		v0 := vd
		battle_object_get_x_by_v0
		i := long tile_attack_highlight_data
		sprite v0 v1 0

: battle_enemy_tick_next
		vd += 1
		if vd != 4 then
	again
	return

: battle_object_count
	4

: battle_object_t
	0
	0
	0
	0

: battle_object_speed
	50
	100
	40
	40

: battle_object_hp
	255
	255
	255
	255

: battle_get_attack_tile
	i := long tile_attack_shot_data
	va += va
	va += va
	va += va
	va += va
	i += va
	return

: battle_attack_table
	jump battle_do_attack
	jump battle_do_engi
	jump battle_do_repair
	jump battle_do_hack

: battle_attack
	battle_get_current_skill
	v0 += v0
	jump0 battle_attack_table

: battle_base_damage_table
	20
	50
	80
	127

: battle_object_damage
	if va == 0 then #MISS
		return
	i := battle_object_hp
	i += vd
	load v0 - v0
	if v0 == 0 then
		return

	v0 -= va
	if vf == 0 begin
		va := sfx_index_explosion
		sfx_play_sync

		battle_object_draw
		v0 := 0
		i := battle_object_hp
		i += vd
	end
	save v0 - v0

	if v0 == 0 then
		battle_object_draw
	return

: battle_do_attack
	battle_get_current_attack
	v0 += 1
	va := v0
	battle_object_attack_animation

	battle_get_current_attack
	i := battle_base_damage_table
	i += v0
	load va - va
	vb := random 0xff
	f8_mul
	va := v0
	jump battle_object_damage

: battle_do_repair
	#repair
	battle_draw_hp_bar
	battle_get_current_attack
	i := battle_base_damage_table
	i += v0
	load va - va
	va <<= va
	vb := random 0xff
	f8_mul
	i := player_hp
	load v1 - v1
	v1 += v0
	if vf != 0 then
		v1 := 255
	save v1 - v1
	battle_draw_hp_bar
	va := sfx_index_powerup
	sfx_play_sync
	return

: battle_do_engi
: battle_do_hack
	return
