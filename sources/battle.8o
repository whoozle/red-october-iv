:const battle_menu_x 10
:const battle_menu_y 34
:const sfx_index_menu 0

: menu_check_key
	ve := va
	if ve key begin
		va := sfx_index_menu
		sfx_play
		loop
			sound_tick
			if ve key then
		again
		v0 := 1
		return
	end
	battle_tick
	v0 := 0
	return

: battle_menu_handle_key
	menu_check_key
	if v0 != 0 begin
		battle_menu_draw_highlight
		i := battle_menu_pos
		load v0 - v0
		v0 += vb
		if v0 > 3 then
			v0 := vc
		save v0 - v0
		battle_menu_draw_highlight
	end
	return


: battle_menu
	v0 := 0
	i := battle_menu_pos
	save v0 - v0

	va := battle_menu_x
	vb := 34
	vc := text_battle_menu_attack

	draw_text_ns
	va := battle_menu_x
	vb := 41
	vc := text_battle_menu_skill
	draw_text_ns

	va := battle_menu_x
	vb := 48
	vc := text_battle_menu_charge
	draw_text_ns

	va := battle_menu_x
	vb := 55
	vc := text_battle_menu_escape
	draw_text_ns

	battle_menu_draw_highlight

	loop
		va := 5
		vb := -1
		vc := 3
		battle_menu_handle_key

		va := 8
		vb := 1
		vc := 0
		battle_menu_handle_key
	again

: battle_menu_highlight
0xff 0xff 0xff 0xff 0xff 0xff 0xff 0
0xff 0xff 0xff 0xff 0xff 0xff 0xff 0

: battle_menu_pos
	0

: battle_menu_draw_highlight
	i := battle_menu_pos
	load v0 - v0
	v1 := v0
	v1 += v1
	v1 += v1
	v1 += v1 # * 8
	v1 -= v0 # * 7
	v1 += battle_menu_y
	v0 := battle_menu_x
	v0 += -1
	v1 += -1
	i := battle_menu_highlight
	v2 := 6
	loop
		sprite v0 v1 8
		v0 += 8
		v2 += -1
		if v2 != 0 then
	again
	return

:const battle_tick_freq_base 10

: battle_tick_last_t
	0

: battle_tick_delay
	0
	0
	0
	0

: battle_tick
	i := battle_tick_last_t
	load v0 - v0
	v1 := delay
	if v1 == 0 begin
		v1 := 6
		delay := v1
	end
	if v1 == v0 then
		return
	save v1 - v1

	vd := 0
	loop
		battle_tick_object
		vd += 1
		if vd != 4 then
	again
	return

: battle_tick_object
	i := battle_tick_delay
	i += vd
	load v0 - v0
	if v0 != 0 begin
		v0 += -1
		save v0 - v0
		return
	end
	v0 := random 0x3
	v0 += battle_tick_freq_base
	save v0 - v0

	i := long tile_robot_tank_front_data
	v0 := vd
	v0 <<= v0
	v0 <<= v0
	v0 <<= v0
	v0 <<= v0
	v0 <<= v0
	v1 := 10
	sprite v0 v1 0

	return
