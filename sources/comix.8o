: comix_start
	i := game_finished
	load v0 - v0
	if v0 != 0 then
		jump comix_outro

	i := intro_shown
	load v0 - v0
	if v0 == 0 begin
		v0 := 1
		save v0 - v0
		comix_intro
		va := 0
		jump set_current_overlay
	end
	jump comix_game_over

: comix_screen_draw
	clear
	v1 := 0
	v2 := 64
	loop
		v0 := 0
		loop
			sprite v0 v1 0
			i += v2
			v0 += 16
			if v0 != 64 then
		again
		v1 += 16
		if v1 != 32 then
	again
	return

:macro text_line X Y T {
	va := X
	vb := Y
	vc := T
	draw_text
	va := 15
	sleep
}

:const comix_text_x1 66
:const comix_text_x2 2

: comix_intro_1
	i := long tile_submarine_data
	comix_screen_draw

	text_line comix_text_x1 6 text_intro_1_1
	text_line comix_text_x1 16 text_intro_1_2
	text_line comix_text_x1 26 text_intro_1_3
	text_line comix_text_x2 36 text_intro_1_4
	text_line comix_text_x2 46 text_intro_1_5
	text_line comix_text_x2 56 text_intro_1_6
	va := 200
	jump sleep

: comix_intro_2
	i := long tile_underthesea_data
	comix_screen_draw

	text_line comix_text_x1 6 text_intro_2_1
	text_line comix_text_x1 16 text_intro_2_2
	text_line comix_text_x1 26 text_intro_2_3
	text_line comix_text_x2 36 text_intro_2_4
	text_line comix_text_x2 46 text_intro_2_5
	text_line comix_text_x2 56 text_intro_2_6
	va := 200
	jump sleep

: comix_intro_3
	i := long tile_space_data
	comix_screen_draw

	text_line comix_text_x1 6 text_intro_3_1
	text_line comix_text_x1 16 text_intro_3_2
	text_line comix_text_x1 26 text_intro_3_3
	text_line comix_text_x2 36 text_intro_3_4
	text_line comix_text_x2 46 text_intro_3_5
	text_line comix_text_x2 56 text_intro_3_6
	va := 200
	jump sleep

: comix_intro
	clear
	i := long tile_splash_data
	draw_fullscreen_tile
	v0 := key

	clear
	va := 14
	vb := 30
	vc := text_intro_1
	draw_text
	va := 250
	sleep

	comix_intro_1
	comix_intro_2
	comix_intro_3

	va := overlay_overworld
	jump set_current_overlay

: comix_game_over
	i := long tile_crushed_data
	comix_screen_draw

	text_line 75 14 text_game_over_1
	va := 60
	sleep

	va := comix_text_x2
	vb := 36
	vc := text_game_over_2
	draw_text_ns

	va := comix_text_x2
	vb := 46
	vc := text_game_over_3
	draw_text_ns

	va := comix_text_x2
	vb := 56
	vc := text_game_over_4
	draw_text_ns

	loop
		v0 := key
		if v0 == OCTO_KEY_R begin
			va := 0
			jump set_current_overlay
		end
		if v0 == OCTO_KEY_C begin
			v0 := 0
			v1 := 0
			v2 := 0
			v3 := 0
			v4 := 0
			v5 := 0
			v6 := 0
			saveflags v6
		end
		if v0 == OCTO_KEY_X then
			exit
	again

: comix_outro
	i := long tile_nuke_data
	comix_screen_draw

	vd := 0
	loop
		va := sfx_index_explosion
		sfx_play
		scroll-left
		sound_tick
		v0 := delay
		loop
			vf := delay
			if vf == v0 then
		again

		scroll-right
		sound_tick
		v0 := delay
		loop
			vf := delay
			if vf == v0 then
		again

		scroll-up 8
		sound_tick
		v0 := delay
		loop
			vf := delay
			if vf == v0 then
		again

		scroll-down 8
		sound_tick
		v0 := delay
		loop
			vf := delay
			if vf == v0 then
		again

		vd += 1
		if vd != 40 then
	again

	i := long tile_nuke_data
	comix_screen_draw

	text_line comix_text_x1 6 text_win_1
	text_line comix_text_x1 16 text_win_2
	text_line comix_text_x1 26 text_win_3
	text_line comix_text_x2 36 text_win_4
	text_line comix_text_x2 46 text_win_5
	va := 120
	sleep
	text_line comix_text_x2 56 text_win_6
	va := 200
	sleep

	loop
	morse_long
	morse_short
	morse_long
	morse_short
	morse_pause
	morse_long
	morse_long
	morse_short
	morse_long
	morse_pause
	morse_pause
	morse_pause
	morse_pause
	again

: morse_sound
	0xaa 0xaa 0xaa 0xaa
	0xaa 0xaa 0xaa 0xaa
	0xaa 0xaa 0xaa 0xaa
	0xaa 0xaa 0xaa 0xaa


: morse_pause
	va := 20
	sleep
	return

: morse_short_pause
	va := 9
	sleep
	return

: morse_short
	i := morse_sound
	vf := 7
	audio_play_sync_vf
	jump morse_short_pause

: morse_long
	i := morse_sound
	vf := 20
	audio_play_sync_vf
	jump morse_short_pause
