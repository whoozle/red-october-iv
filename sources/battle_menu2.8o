:const battle_menu2_x 60
:const battle_menu2_y 34

: battle_menu2_data
: battle_menu_attack_data
	:byte text_battle_menu_attack_bash
	:byte text_battle_menu_attack_kick
	:byte text_battle_menu_attack_thrust
	:byte text_battle_menu_attack_grapple

: battle_menu_engi_data
	:byte text_battle_menu_engi_solder
	:byte text_battle_menu_engi_sc
	:byte text_battle_menu_engi_dox
	:byte text_battle_menu_engi_defuse

: battle_menu_repair_data
	:byte text_battle_menu_repair_fix
	:byte text_battle_menu_repair_solder
	:byte text_battle_menu_repair_static_shield
	:byte text_battle_menu_repair_disable_ai

: battle_menu_hack_data
	:byte text_battle_menu_hack_underclock
	:byte text_battle_menu_hack_ice_shield
	:byte text_battle_menu_hack_erase_data
	:byte text_battle_menu_hack_erase_system

: battle_skill_level
: battle_attack_level
	1
: battle_engi_level
	1
: battle_repair_level
	1
: battle_hack_level
	1

: battle_get_current_skill
	i := battle_menu_current_skill
	load v0 - v0
	return

: battle_get_current_attack
	i := battle_menu2_pos
	load v0 - v0
	return

: battle_get_current_skill_level
	battle_get_current_skill
	i := battle_skill_level
	i += v0
	load v0 - v0
	return

: battle_menu2_handle_key
	menu_check_key
	if v0 != 0 begin
		battle_menu2_draw_highlight
		battle_get_current_skill_level
		v1 := v0
		i := battle_menu2_pos
		load v0 - v0
		v0 += vb
		if v0 >= v1 then
			v0 := vc
		save v0 - v0
		battle_menu2_draw_highlight
	end
	return

: battle_menu2_draw
	i := battle_menu_current_skill
	load v2 - v2
	v2 += v2
	v2 += v2
	:unpack 0xa battle_menu2_data
	v1 += v2
	v0 += vf

	i := menu_data_ptr
	save v0 - v1

	v0 := battle_menu2_x
	i := menu_x_ptr
	save v0 - v0

	battle_get_current_skill_level
	i := menu_draw_n
	save v0 - v0

	jump menu_draw

: battle_menu_2
	battle_menu2_draw
	battle_menu2_draw_highlight

	loop
		va := OCTO_KEY_W
		vb := -1
		battle_get_current_skill_level
		v0 += -1
		vc := v0
		battle_menu2_handle_key

		va := OCTO_KEY_S
		vb := 1
		vc := 0
		battle_menu2_handle_key

		va := OCTO_KEY_Q
		vb := 0
		vc := 0
		battle_menu2_handle_key
		if v0 != 0 begin
			v9 := 0
			jump battle_menu2_exit
		end

		va := OCTO_KEY_E
		menu_check_key
		if v0 != 0 begin
			battle_menu_pick_target
			if v0 != -1 begin
				vd := v0
				battle_object_draw_hp_bar
				battle_attack
				battle_object_draw_hp_bar
				v9 := 1
				jump battle_menu2_exit
			end
		end
	again

: battle_menu2_exit
	battle_menu2_draw_highlight
	battle_menu2_draw
	v0 := v9
	return

: battle_menu2_pos
	0

: battle_menu2_draw_highlight
	i := battle_menu2_pos
	load v0 - v0
	v1 := v0
	v1 += v1
	v1 += v1
	v1 += v1 # * 8
	v1 -= v0 # * 7
	v1 += battle_menu2_y
	v0 := battle_menu2_x
	v0 += -1
	v1 += -1
	i := long tile_highlight_menu_data
	v2 := 8
	loop
		sprite v0 v1 8
		v0 += 8
		v2 += -1
		if v2 != 0 then
	again
	return
