: menu_check_key
	ve := va
	if ve key begin
		va := sfx_index_menu
		sfx_play
		loop
			sound_tick
			if ve key then
		again
		v0 := 1
		return
	end
	battle_tick
	v0 := 0
	return

: battle_menu_handle_key
	menu_check_key
	if v0 != 0 begin
		battle_menu_draw_highlight
		i := battle_menu_pos
		load v0 - v0
		v0 += vb
		if v0 > 3 then
			v0 := vc
		save v0 - v0
		battle_menu_draw_highlight
	end
	return


: battle_menu
	clear
	battle_draw_init

	v0 := 0
	i := battle_menu_pos
	save v0 - v0

	va := battle_menu_x
	vb := 34
	vc := text_battle_menu_attack

	draw_text_ns
	va := battle_menu_x
	vb := 41
	vc := text_battle_menu_skill
	draw_text_ns

	va := battle_menu_x
	vb := 48
	vc := text_battle_menu_charge
	draw_text_ns

	va := battle_menu_x
	vb := 55
	vc := text_battle_menu_escape
	draw_text_ns

	battle_menu_draw_highlight

	loop
		va := OCTO_KEY_W
		vb := -1
		vc := 3
		battle_menu_handle_key

		va := OCTO_KEY_S
		vb := 1
		vc := 0
		battle_menu_handle_key
		va := 6

		menu_check_key
		if v0 != 0 then
			battle_menu_pick_target
	again

: battle_menu_pos
	0

: battle_menu_draw_highlight
	i := battle_menu_pos
	load v0 - v0
	v1 := v0
	v1 += v1
	v1 += v1
	v1 += v1 # * 8
	v1 -= v0 # * 7
	v1 += battle_menu_y
	v0 := battle_menu_x
	v0 += -1
	v1 += -1
	i := long tile_highlight_menu_data
	v2 := 6
	loop
		sprite v0 v1 8
		v0 += 8
		v2 += -1
		if v2 != 0 then
	again
	return

: battle_tick_last_t
	0

: battle_object_count
	4

: battle_tick_delay
	0
	0
	0
	0

: battle_object_animation
	0
	0
	0
	0

0xde 0xde 0xde
: battle_object_type
	:byte battle_robot_tank
	:byte battle_robot_engineer
	:byte battle_robot_hacker
	:byte battle_robot_repairbot

#inline it?
: battle_object_get_tile
	i := battle_object_type
	i += vd
	load v0 - v0
	v0 += v0

	i := long battle_object_tiles
	i += v0
	load v0 - v1

	i := battle_object_get_tile_addr
	save v0 - v1

	i := battle_object_animation
	i += vd
	load v0 - v0

	0xf0 0x00
: battle_object_get_tile_addr
	0 0
	i += v0
	return

: battle_object_get_x_by_v0
	v0 <<= v0
	v0 <<= v0
	v0 <<= v0
	v1 := v0
	v0 <<= v0
	v0 += v1
	v1 := 10
	v0 += 16
	return

: battle_object_draw
	battle_object_get_tile

	v0 := vd
	battle_object_get_x_by_v0
	sprite v0 v1 0
	return

: battle_object_swap_tiles
	i := battle_object_animation
	i += vd
	load v0 - v0
	v1 := 64
	v0 =- v1
	save v0 - v0
	return

: battle_object_tick
	i := battle_tick_delay
	i += vd
	load v0 - v0
	if v0 != 0 begin
		v0 += -1
		save v0 - v0
		return
	end
	v0 := random battle_tick_freq_random
	v0 += battle_tick_freq_base
	save v0 - v0

	battle_object_draw
	battle_object_swap_tiles
	battle_object_draw
	return

: battle_draw_init
	vd := 0
	loop
		battle_object_draw
		vd += 1
		i := battle_object_count
		load v0 - v0
		if vd != v0 then
	again
	return

: battle_tick
	i := battle_tick_last_t
	load v0 - v0
	v1 := delay
	if v1 == 0 begin
		v1 := 6
		delay := v1
	end
	if v1 == v0 then
		return
	save v1 - v1

	vd := 0
	loop
		battle_object_tick
		vd += 1
		if vd != 4 then
	again
	return

#==== ATTACK =============================================
: battle_object_draw_highlight
	i := battle_menu_pick_target_target
	load v0 - v0
	battle_object_get_x_by_v0
	i := long tile_highlight_16_data
	sprite v0 v1 0
	return

: battle_menu_pick_target_target
	0

: battle_menu_pick_target
	va := 0
	i := battle_menu_pick_target_target
	save va - va
	battle_object_draw_highlight

	loop
		sound_tick
		va := OCTO_KEY_A
		vb := -1
		vc := 3
		battle_menu_pick_target_handle_key
		va := OCTO_KEY_D
		vb := 1
		vc := 0
		battle_menu_pick_target_handle_key
		v0 := 4
		if v0 -key then
	again
	va := sfx_index_menu
	sfx_play
	battle_object_draw_highlight
	return

: battle_menu_pick_target_handle_key
	menu_check_key
	if v0 != 0 begin
		battle_object_draw_highlight
		i := battle_object_count
		load v1 - v1

		i := battle_menu_pick_target_target
		load v0 - v0
		v0 += vb
		if v0 >= v1 then
			v0 := vc
		save v0 - v0
		battle_object_draw_highlight
	end
	return
