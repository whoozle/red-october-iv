:const battle_menu_x 10
:const battle_menu_y 34

: menu_check_key
	ve := va
	if ve key begin
		va := sfx_index_menu
		sfx_play
		loop
			sound_tick
			if ve key then
		again
		v0 := 1
		return
	end
	battle_tick
	v0 := 0
	return

: battle_menu_handle_key
	menu_check_key
	if v0 != 0 begin
		battle_menu_draw_highlight
		i := battle_menu_current_skill
		load v0 - v0
		v0 += vb
		if v0 > 3 then
			v0 := vc
		save v0 - v0
		battle_menu_draw_highlight
		battle_reset_current_attack
	end
	return

: battle_menu_data
	:byte text_battle_menu_attack
	:byte text_battle_menu_engi
	:byte text_battle_menu_repair
	:byte text_battle_menu_hack

: menu_draw
	vd := 0
	loop
	: menu_data_ptr
		i := 0
		i += vd
		load vc - vc

		:next menu_x_ptr
		va := 0
		vb := vd
		vb += vb
		vb += vb
		vb += vb
		vb -= vd #Ã—7
		vb += 34

		draw_text_ns

		vd += 1
		:next menu_draw_n
		if vd == 4 then
			return
	again

: battle_menu_draw
	:unpack 0xa battle_menu_data
	i := menu_data_ptr
	save v0 - v1
	v0 := battle_menu_x
	i := menu_x_ptr
	save v0 - v0
	i := menu_draw_n
	v0 := 4
	save v0 - v0
	jump menu_draw

: battle_menu
	battle_draw_init

	v0 := 0
	i := battle_menu_current_skill
	save v0 - v0

	battle_menu_draw
	battle_menu_draw_highlight

	loop
		va := OCTO_KEY_W
		vb := -1
		vc := 3
		battle_menu_handle_key

		va := OCTO_KEY_S
		vb := 1
		vc := 0
		battle_menu_handle_key

		va := OCTO_KEY_E
		vb := 0
		vb := 0
		menu_check_key
		if v0 != 0 begin
			battle_menu_2
			if v0 != 0 then
				battle_enemy_tick

			battle_is_active
			if v0 == 0 begin #you have won the battle
				i := current_battle
				load v0 - v0

				i := encounters
				i += v0
				v0 := 0
				save v0 - v0 #removing current battle flag

				i := encounters
				v1 := 0
				v2 := 0
				loop
					load v0
					v2 |= v0
					v1 += 1
					if v1 != 16 then
				again

				if v2 == 0 begin # no encounters left, won the game
					i := game_finished
					v0 := 1
					save v0 - v0

					va := overlay_comix
					jump set_current_overlay
				end

				va := overlay_overworld
				jump set_current_overlay
			end
			player_is_alive
			if v0 == 0 begin
				va := overlay_comix
				jump set_current_overlay
			end
		end
	again

: battle_menu_current_skill
	0

: battle_menu_draw_highlight
	i := battle_menu_current_skill
	load v0 - v0
	v1 := v0
	v1 += v1
	v1 += v1
	v1 += v1 # * 8
	v1 -= v0 # * 7
	v1 += battle_menu_y
	v0 := battle_menu_x
	v0 += -1
	v1 += -1
	i := long tile_highlight_menu_data
	v2 := 6
	loop
		sprite v0 v1 8
		v0 += 8
		v2 += -1
		if v2 != 0 then
	again
	return
