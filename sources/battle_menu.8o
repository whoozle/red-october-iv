: menu_check_key
	ve := va
	if ve key begin
		va := sfx_index_menu
		sfx_play
		loop
			sound_tick
			if ve key then
		again
		v0 := 1
		return
	end
	battle_tick
	v0 := 0
	return

: battle_menu_handle_key
	menu_check_key
	if v0 != 0 begin
		battle_menu_draw_highlight
		i := battle_menu_pos
		load v0 - v0
		v0 += vb
		if v0 > 3 then
			v0 := vc
		save v0 - v0
		battle_menu_draw_highlight
	end
	return


: battle_menu
	clear
	battle_draw_init

	v0 := 0
	i := battle_menu_pos
	save v0 - v0

	va := battle_menu_x
	vb := 34
	vc := text_battle_menu_attack

	draw_text_ns
	va := battle_menu_x
	vb := 41
	vc := text_battle_menu_engi
	draw_text_ns

	va := battle_menu_x
	vb := 48
	vc := text_battle_menu_repair
	draw_text_ns

	va := battle_menu_x
	vb := 55
	vc := text_battle_menu_hack
	draw_text_ns

	battle_menu_draw_highlight

	loop
		va := OCTO_KEY_W
		vb := -1
		vc := 3
		battle_menu_handle_key

		va := OCTO_KEY_S
		vb := 1
		vc := 0
		battle_menu_handle_key
		va := 6

		menu_check_key
		if v0 != 0 begin
			battle_menu_pick_target
			if v0 != -1 begin
				vd := v0
				battle_object_draw_hp_bar
				battle_attack
				battle_object_draw_hp_bar
				va := sfx_index_hit
				sfx_play
			end
		end
	again

: battle_menu_pos
	0

: battle_menu_draw_highlight
	i := battle_menu_pos
	load v0 - v0
	v1 := v0
	v1 += v1
	v1 += v1
	v1 += v1 # * 8
	v1 -= v0 # * 7
	v1 += battle_menu_y
	v0 := battle_menu_x
	v0 += -1
	v1 += -1
	i := long tile_highlight_menu_data
	v2 := 6
	loop
		sprite v0 v1 8
		v0 += 8
		v2 += -1
		if v2 != 0 then
	again
	return

: battle_draw_init
	vd := 0
	loop
		battle_object_draw
		battle_object_draw_hp_bar
		vd += 1
		i := battle_object_count
		load v0 - v0
		if vd != v0 then
	again
	return

#==== ATTACK =============================================
: battle_object_draw_highlight
	i := battle_menu_target
	load v0 - v0
	battle_object_get_x_by_v0
	i := long tile_highlight_16_data
	sprite v0 v1 0
	return

: battle_menu_target
	-1

: battle_menu_pick_target
	va := 0
	i := battle_menu_target
	save va - va
	battle_object_draw_highlight

	loop
		sound_tick
		va := OCTO_KEY_A
		vb := -1
		vc := 3
		battle_menu_pick_target_handle_key

		va := OCTO_KEY_D
		vb := 1
		vc := 0
		battle_menu_pick_target_handle_key

		va := OCTO_KEY_E
		menu_check_key
		if v0 != 0 then
			jump battle_menu_pick_target_ok

		va := OCTO_KEY_Q
		menu_check_key
		if v0 == 0 then
	again

	va := sfx_index_menu
	sfx_play
	battle_object_draw_highlight
	v0 := -1
	return

: battle_menu_pick_target_ok
	battle_object_draw_highlight
	i := battle_menu_target
	load v0 - v0
	return

: battle_menu_pick_target_handle_key
	menu_check_key
	if v0 != 0 begin
		battle_object_draw_highlight
		i := battle_object_count
		load v1 - v1

		i := battle_menu_target
		load v0 - v0
		v0 += vb
		if v0 >= v1 then
			v0 := vc
		save v0 - v0
		battle_object_draw_highlight
	end
	return
